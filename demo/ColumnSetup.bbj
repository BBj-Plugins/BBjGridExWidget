rem /**
rem  * This file is part of the BBjGridExWidget plugin.
rem  * (c) Basis Europe <eu@basis.com>
rem  *
rem  * For the full copyright and license information, please view the LICENSE
rem  * file that was distributed with this source code.
rem  */
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.db.ResultSet
use com.basiscomponents.bc.SqlQueryBC
use com.google.gson.JsonObject
use java.sql.Types

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare BBjGridExWidget grid!

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"Column Setup Demo")
wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!.setCallback(BBjAPI.ON_RESIZE,"resize")

REM init the grid
grid! = new BBjGridExWidget(wnd!,100,0,0,800,600)

gosub main
process_events

rem /**
rem  * Retrieve the data from the database and configure the grid.
rem  *
rem  * This routine shows how to configure the different column attributes using the Columns Manager methods.
rem  * Most of these method work at the configuration level, which means you need to call them before calling
rem  * grid!.setData() or grid!.render(). Some of them, like grid!.moveColumn() and grid!.pinColumn()
rem  * can call the client API to force the change, even if the grid is already rendered.
rem  * To know which methods can do this and which cannot, check the plugin API.
rem  */
main:
  declare SqlQueryBC sbc!
  declare ResultSet rs!

  sbc! = new SqlQueryBC(BBjAPI().getJDBCConnection("CDStore"))
  rs! = sbc!.retrieve("SELECT  CDNUMBER , ARTIST, TITLE, LABEL, PLAYINGTIME FROM CDINVENTORY")

  grid!.addColumn("CDNUMBER","Item#")
  grid!.addColumn("ARTIST","Artist Name")
  grid!.addColumn("TITLE","Record Title")
  grid!.addColumn("LABEL","Publisher")
  grid!.addColumn("PLAYINGTIME","Playing Time (min)").setBackColor("lightgreen")

  grid!.setColumnBackColor("CDNUMBER",BBjAPI().makeColor("LTGRAY"))
  grid!.setColumnForeColor("CDNUMBER",BBjAPI().makeColor("BLUE"))
  grid!.pinColumn("CDNUMBER","left")
  grid!.setColumnWidth("CDNUMBER",120)
  grid!.setColumnAlignment("CDNUMBER",grid!.GRID_ALIGN_CENTER())

  grid!.setData(rs!,"CDNUMBER")

  rem You can move columns after rendering
  grid!.moveColumn("PLAYINGTIME",1)
return

rem /**
rem  * Listen to the BBjTopLevelWindow resize events and
rem  * resize the grid to fill the available space.
rem  */
resize:
  ev! = BBjAPI().getLastEvent()
  w=ev!.getWidth()
  h=ev!.getHeight()
  grid!.setSize(w,h)
return

rem /**
rem  * Close the demo
rem  */
byebye:
bye
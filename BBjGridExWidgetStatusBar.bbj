rem package BBjGridExWidget.StatusBar
rem /**
rem  * This file is part of the BBjGridExWidget plugin.
rem  * (c) Basis Europe <eu@basis.com>
rem  *
rem  * For the full copyright and license information, please view the LICENSE
rem  * file that was distributed with this source code.
rem  */
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use java.util.LinkedHashMap
use com.google.gson.JsonArray
use com.google.gson.JsonObject
use com.google.gson.Gson
use java.util.HashSet
rem /**
rem  * A Interface which defines a status bar component
rem  *
REM  * @author Hyyan Abo Fakher
rem  */
interface public BBjGridExWidgetStatusBarComponentInterface
    rem /**
    rem  * Get the component name
    rem  *
    rem  * @returns BBjString the component name
    rem  */

    method public static BBjString NAME()
    rem /**
    rem  * Set the component alignment
    rem  *
    rem  * @param BBjString alignment$ "right" , "left" or "center"
    rem  */

    method public void setAlignment(BBjString alignment!)
    rem /**
    rem  * Get the component alignment
    rem  *
    rem  * @returns BBjString the component alignment
    rem  */

    method public BBjString getAlignment()
    rem /**
    rem  * Get The component's JSON representation
    rem  *
    rem  * @return JsonObject
    rem  */

    method public JsonObject getAsJsonObject()

interfaceend
rem /**
rem  * An Abstract implementation for BBjGridExWidgetStatusBarComponentInterface
rem  *
REM  * @author Hyyan Abo Fakher
rem  */
class public BBjGridExWidgetStatusBarComponent implements BBjGridExWidgetStatusBarComponentInterface
    rem /**
    rem  * The component alignment
    rem  */
    field public BBjString Alignment! = "left"
    rem /**
    rem  * Constructor
    rem  *
    rem  * @param BBjString alignment! "right" , "left" or "center"
    rem  */
    method public BBjGridExWidgetStatusBarComponent(BBjString alignment!)
        #Alignment! = alignment!
    methodend
    rem /**
    rem  * Get The component's JSON representation
    rem  *
    rem  * @return JsonObject
    rem  */
    method public JsonObject getAsJsonObject()
        declare JsonObject json!

        json! = new JsonObject()
        json!.addProperty("statusPanel",#this!.NAME())
        json!.addProperty("align",#getAlignment())
        methodret json!
    methodend

classend
rem /**
rem  * A statusbar component which provides the selected row count
rem  *
REM  * @author Hyyan Abo Fakher
rem  */
class public BBjGridExWidgetStatusBarSelectedRowCountComponent extends BBjGridExWidgetStatusBarComponent
    rem /**
    rem  * Constructor
    rem  *
    rem  * @param BBjString alignment! "right" , "left" or "center"
    rem  */
    method public BBjGridExWidgetStatusBarSelectedRowCountComponent(BBjString alignment!)
        #super!(alignment!)
    methodend
    rem /**
    rem  * {@inheritDoc}
    rem  */
    method public static BBjString NAME()
        methodret "agSelectedRowCountComponent"
    methodend

classend
rem /**
rem  * A statusbar component which provides the filtered row count.
rem  *
REM  * @author Hyyan Abo Fakher
rem  */
class public BBjGridExWidgetStatusBarFilteredRowCountComponent extends BBjGridExWidgetStatusBarComponent
    rem /**
    rem  * Constructor
    rem  *
    rem  * @param BBjString alignment! "right" , "left" or "center"
    rem  */
    method public BBjGridExWidgetStatusBarFilteredRowCountComponent(BBjString alignment!)
        #super!(alignment!)
    methodend
    rem /**
    rem  * {@inheritDoc}
    rem  */
    method public static BBjString NAME()
        methodret "agFilteredRowCountComponent"
    methodend

classend
rem /**
rem  * A statusbar component which provides the total and filtered row count.
rem  *
REM  * @author Hyyan Abo Fakher
rem  */
class public BBjGridExWidgetStatusBarTotalAndFilteredRowCountComponent extends BBjGridExWidgetStatusBarComponent
    rem /**
    rem  * Constructor
    rem  *
    rem  * @param BBjString alignment! "right" , "left" or "center"
    rem  */
    method public BBjGridExWidgetStatusBarTotalAndFilteredRowCountComponent(BBjString alignment!)
        #super!(alignment!)
    methodend
    rem /**
    rem  * {@inheritDoc}
    rem  */
    method public static BBjString NAME()
        methodret "agTotalAndFilteredRowCountComponent"
    methodend

classend
rem /**
rem  * A statusbar component which provides the total row count.
rem  *
REM  * @author Hyyan Abo Fakher
rem  */
class public BBjGridExWidgetStatusBarTotalRowCountComponent extends BBjGridExWidgetStatusBarComponent
    rem /**
    rem  * Constructor
    rem  *
    rem  * @param BBjString alignment! "right" , "left" or "center"
    rem  */
    method public BBjGridExWidgetStatusBarTotalRowCountComponent(BBjString alignment!)
        #super!(alignment!)
    methodend
    rem /**
    rem  * {@inheritDoc}
    rem  */
    method public static BBjString NAME()
        methodret "agTotalRowCountComponent"
    methodend

classend
rem /**
rem  * A statusbar component which provides aggregations on the selected range.
rem  *
REM  * @author Hyyan Abo Fakher
rem  */
class public BBjGridExWidgetStatusBarAggregationComponent extends BBjGridExWidgetStatusBarComponent
    rem /**
    rem  * agg fucntions to show on the panel. possible values are: 'count', 'sum', 'min', 'max', 'avg'
    rem  */
    field public HashSet AggFuncs! =  new HashSet()
    rem /**
    rem  * Construct new Aggregation Component
    rem  */
    method public BBjGridExWidgetStatusBarAggregationComponent()
        #fillHashSetWithDefault()
    methodend
    rem /**
    rem  * Construct new Aggregation Component
    rem  *
    rem  * @param BBjString alignment! "right" , "left" or "center"
    rem  */
    method public BBjGridExWidgetStatusBarAggregationComponent(BBjString alignment!)
        #super!(alignment!)
        #fillHashSetWithDefault()
    methodend
    rem /**
    rem  * {@inheritDoc}
    rem  */
    method public static BBjString NAME()
        methodret "agAggregationComponent"
    methodend
    rem /**
    rem  * @override
    rem  *
    rem  * {@inheritDoc}
    rem  */
    method public JsonObject getAsJsonObject()
        params! = new JsonObject()
        params!.addProperty("aggFuncs" , iff(#getAggFuncs().size() <> 0 ,new Gson().toJson(#getAggFuncs()), null()) ,err=*next)
        json! = #super!.getAsJsonObject()
        json!.add("statusPanelParams",params!)
        methodret json!
    methodend

    method protected void fillHashSetWithDefault()
        #getAggFuncs().add("count")
        #getAggFuncs().add("sum")
        #getAggFuncs().add("min")
        #getAggFuncs().add("max")
        #getAggFuncs().add("avg")
    methodend

classend
rem /**
rem  * BBjGridExWidgetStatusBar
rem  *
rem  * BBjGridExWidget status bar manager.
rem  * The class can add , remove & hide status bar components
rem  *
rem  * @Enterprise
rem  */
class public BBjGridExWidgetStatusBar
    rem /**
    rem  *  A map for the registered components
    rem  */
    field protected LinkedHashMap Components! = new LinkedHashMap()
    rem /**
    rem  * The BBjGridExWidget instance
    rem  */
    field protected BBjGridExWidget Widget!
    rem /**
    rem  * Construct new sidebar
    rem  *
    rem  * @param BBjGridExWidget widget!
    rem  */
    method public BBjGridExWidgetStatusBar(BBjGridExWidget widget!)
        #Widget! = widget!
    methodend
    rem /**
    rem  * Add statusbar component
    rem  *
    rem  * @param BBjGridExWidgetStatusBarComponentInterface component! a statusbar component
    rem  */
    method public void addComponent(BBjGridExWidgetStatusBarComponentInterface component!)
        #Components!.put(component!.NAME(),component!)
    methodend
    rem /**
    rem  * Get statusbar component
    rem  *
    rem  * @param BBjString name! the component name
    rem  */
    method public BBjGridExWidgetStatusBarComponentInterface getComponent(BBjString name!)
        declare auto BBjGridExWidgetStatusBarComponentInterface c!

        c! = #Components!.get(name!)
        methodret c!
    methodend
    rem /**
    rem  * Remove statusbar compoent
    rem  *
    rem  * @param BBjString name! the component name
    rem  */
    method public void removeComponent(BBjString name!)
        #Components!.remove(name!)
    methodend
    rem /**
    rem  * Remove statusbar component
    rem  *
    rem  * @param BBjGridExWidgetStatusBarComponentInterface component! a statusbar component
    rem  */
    method public void removeComponent(BBjGridExWidgetStatusBarComponentInterface component!)
        #Components!.remove(component!.NAME())
    methodend
    rem /**
    rem  * Hide/show statusbar component
    rem  *
    rem  * @param BBjGridExWidgetStatusBarComponentInterface component! a statusbar component
    rem  * @param BBjNumber visiblity! 0 to hide component , 1 to show compoent
    rem  */
    method public void setComponentVisibility(BBjGridExWidgetStatusBarComponentInterface component! , BBjNumber visiblity!)
        #Widget!.executeScript("gw_setStatusbarComponentVisibility('" + #Widget!.getGRIDID() + "','" + component!.NAME() + "'," + str(visiblity!) + ")")
    methodend
    rem /**
    rem  * Hide/show statusbar component
    rem  *
    rem  * @param BBjString name! the component name
    rem  * @param BBjNumber visiblity! 0 to hide component , 1 to show compoent
    rem  */
    method public void setComponentVisibility(BBjString name! , BBjNumber visiblity!)
        #Widget!.executeScript("gw_setStatusbarComponentVisibility('" + #Widget!.getGRIDID() + "','" + name! + "'," + str(visiblity!) + ")")
    methodend
    rem /**
    rem  * Convert the statusbar into a parsable json string
    rem  *
    rem  * @returns BBjString
    rem  */
    method public BBjString toString()
        declare JsonArray defs!

        defs! = new JsonArray()
        it! = #Components!.entrySet().iterator()
        while it!.hasNext()=BBjAPI.TRUE
            defs!.add(it!.next().getValue().getAsJsonObject())
        wend

        declare JsonObject json!

        json! = new JsonObject()
        json!.addProperty("statusPanels",str(defs!.toString()))
        methodret new Gson().toJson(json!)
    methodend

classend
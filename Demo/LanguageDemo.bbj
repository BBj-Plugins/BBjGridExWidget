rem /**
rem  * This file is part of the BBjGridExWidget plugin.
rem  * (c) Basis Europe <eu@basis.com>
rem  *
rem  * For the full copyright and license information, please view the LICENSE
rem  * file that was distributed with this source code.
rem  */
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.db.ResultSet
use com.basiscomponents.bc.SqlQueryBC

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare auto BBjListButton lb_languages!
declare BBjGridExWidget grid!
declare BBjVector languages!

REM Define a list of supported languages
languages! = new BBjVector()
languages!.insertItem(0,"")
languages!.insertItem(1,"de_DE")
languages!.insertItem(2,"nl_NL")
languages!.insertItem(3,"de_SL"); rem Just a demo language file.

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,900,600,"BBj Grid Language Demo")
wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!.setCallback(BBjAPI.ON_RESIZE,"resize")

lb_languages! = wnd!.addListButton(201,5,6,160,250,"")
lb_languages!.insertItemAt(0,"System Language")
lb_languages!.insertItemAt(1,"German")
lb_languages!.insertItemAt(2,"Dutch")
lb_languages!.insertItemAt(3,"Saarlandish (Custom)")
lb_languages!.selectIndex(0)
lb_languages!.setCallback(BBjAPI.ON_LIST_SELECT,"updateLanguage")

REM init the grid
grid! = new BBjGridExWidget(wnd!,100,0,35,900,566)

gosub main
process_events
rem /**
rem  * Retrive the data from the database and configure the grid
rem  * 
rem  * By default the grid will use stbl("!LOCALE") to get the default language
rem  * Changing this setting will force the grid to use the defined language unless the
rem  * default's grid language is set direclty using grid!.getLanguageManager().setLangauge("LANG")
rem  */
main:
  declare SqlQueryBC sbc!
  declare ResultSet rs!
  
  sbc! = new SqlQueryBC(BBjAPI().getJDBCConnection("CDStore"))
  rs! = sbc!.retrieve("SELECT  * FROM CDINVENTORY")
  REM   add custom languge to the grid
  grid!.getLanguageManager().add("de_SL","BBjGridExWidget/Demo/assets/languages/de_SL.json")
  grid!.setData(rs!)
return

rem /**
rem  * Updating the grid language requires a rerende.
rem  * Note how we are calling grid!.render() here instead of grid!.setData()
rem  */
updateLanguage:
  ev! = BBjAPI().getLastEvent()
  langauge$ = languages!.get(ev!.getSelectedIndex())
  REM setting a new language requires a full rerender
  grid!.getLanguageManager().setLanguage(langauge$)
  grid!.render()
return

rem /**
rem  * Listen to the BBjTopLevelWindow resize events and
rem  * resize the grid to fill the available space. 
rem  */
resize:
  ev! = BBjAPI().getLastEvent()
  w=ev!.getWidth()
  h=ev!.getHeight()
  grid!.setSize(w,h)
return

rem /**
rem  * Close the demo
rem  */
byebye:
bye
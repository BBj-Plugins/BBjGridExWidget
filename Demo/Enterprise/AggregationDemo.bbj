use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.SqlQueryBC
use java.sql.Types
use com.google.gson.JsonObject

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare auto BBjListButton lb_db!
declare auto BBjListButton lb_tbl!
declare auto BBjToolButton btn_fit!
declare BBjGridExWidget grid!

lic!=System.getProperty("bbjgridexwidget.license")

if lic!=null() or str(lic!)="" then
    a=msgbox("set license key in bbj.properties under bbjgridexwidget.license=<yourlicense>",48,"Demo for Enterprise Features")
    release
fi

BBjGridExWidget.setLicenseKey(lic!)

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"BBj Grid Ex Demo")
cb! = wnd!.addCheckBox(203,12,10,200,22,"Enable Read Only Mode")
cb!.setCallback(BBjAPI.ON_CHECK_OFF,"onUseCheckbox")
cb!.setCallback(BBjAPI.ON_CHECK_ON,"onUseCheckbox")
wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

gosub setupGrid

process_events
   
setupGrid:

    declare SqlQueryBC sbc!
    sbc! = new SqlQueryBC(BBjAPI().getJDBCConnection("CDStore"))
    rs! = sbc!.retrieve("SELECT  * FROM CDINVENTORY")

    filter! = new com.basiscomponents.db.DataRow()
    filter!.setFieldValue("RECORDINGTYPE","cond:<DDD&>AAD")
    
    rs! = rs!.filterBy(filter!)
    playingTimeIndex! =rs!.getColumnIndex("PLAYINGTIME")

    rs!.setAttribute(playingTimeIndex!,"ENABLE_VALUE","1")
    rs!.setAttribute(playingTimeIndex!,"AGG_FUNC","sum")
    rs!.setAttribute(playingTimeIndex!,"ALLOWED_AGG_FUNCS","sum,min,max")
    rem convert string to number 
    rs!.setAttribute(playingTimeIndex!,"VALUE_GETTER","Number(data.PLAYINGTIME)")
    rs!.setAttribute(playingTimeIndex!,"FOOTER_VALUE_GETTER","'PlayingTime Total' + '(' + x + ')' ")

    costIndex! =rs!.getColumnIndex("COST")

    rs!.setAttribute(costIndex!,"ENABLE_VALUE","1")
    rs!.setAttribute(costIndex!,"AGG_FUNC","sum")
    rs!.setAttribute(costIndex!,"ALLOWED_AGG_FUNCS","sum,avg")
    rs!.setAttribute(costIndex!,"FOOTER_VALUE_GETTER","if(x) {return column.aggFunc + '(' + BBj.Masks.Types.number(x, '##,##.0' , ',' , '.')  + ') $'} else {return x} ")
    
    grid! = new BBjGridExWidget(wnd!,100,0,40,800,560)

    grid!.setSuppressValues(0)
    grid!.setShowToolPanel(1)
    grid!.setGroupIncludeFooter(1)
    grid!.setGroupIncludeTotalFooter(1)
    grid!.setRowGroupPanelShow(grid!.GRID_GROUPPANEL_SHOW_VISIBLE())
    grid!.setShowGroupChildCount(1)
    grid!.setShowGroupSelectionCheckbox(1)
    grid!.setGroupSelectsChildren(1)
    
    grid!.setData(rs!,"CDNUMBER")
return

onUseCheckbox:
    useCheckbox! = cb!.isSelected()
    grid!.setFunctionsReadOnly(useCheckbox!)
return
    
byebye:
    bye
    
resize:
    ev! = BBjAPI().getLastEvent()
    grid!.setSize(ev!.getWidth(),ev!.getHeight()-40)
return



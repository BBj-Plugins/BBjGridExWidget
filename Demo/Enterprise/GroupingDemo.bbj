use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidgetColumnsToolpanel
use com.basiscomponents.bc.SqlQueryBC
use java.sql.Types
use com.google.gson.JsonObject

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare auto BBjListButton lb_db!
declare auto BBjListButton lb_tbl!
declare auto BBjToolButton btn_fit!
declare BBjGridExWidget grid!

lic!=System.getProperty("bbjgridexwidget.license")

if lic!=null() or str(lic!)="" then
    a=msgbox("set license key in bbj.properties under bbjgridexwidget.license=<yourlicense>",48,"Demo for Enterprise Features")
    release
fi

BBjGridExWidget.setLicenseKey(lic!)

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"BBj Grid Ex Demo")
cb! = wnd!.addCheckBox(203,12,10,200,22,"Enable Read Only Mode")
cb!.setCallback(BBjAPI.ON_CHECK_OFF,"onUseCheckbox")
cb!.setCallback(BBjAPI.ON_CHECK_ON,"onUseCheckbox")
wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

gosub setupGrid

process_events

   
setupGrid:

    declare SqlQueryBC sbc!
    sbc! = new SqlQueryBC(BBjAPI().getJDBCConnection("CDStore"))
    rs! = sbc!.retrieve("SELECT  * FROM CDINVENTORY")

    filter! = new com.basiscomponents.db.DataRow()
    filter!.setFieldValue("RECORDINGTYPE","cond:<DDD&>AAD")
    
    rs! = rs!.filterBy(filter!)
        
    musicTypeIndex! = rs!.getColumnIndex("MUSICTYPE")     
    artistIndex! = rs!.getColumnIndex("ARTIST")
    playingTimeIndex! =rs!.getColumnIndex("PLAYINGTIME")
     
    rs!.setAttribute(musicTypeIndex!,"ROW_GROUP","1")
    rs!.setAttribute(musicTypeIndex!,"ROW_GROUP_INDEX","0")
    
    rs!.setAttribute(artistIndex!,"ROW_GROUP","1")
    rs!.setAttribute(artistIndex!,"ROW_GROUP_INDEX","1")
    rs!.setAttribute(artistIndex!,"VALUE_GETTER","if(data) return 'Artist : ' +  data.ARTIST")

    rs!.setAttribute(playingTimeIndex!,"ENABLE_VALUE","1")
    rs!.setAttribute(playingTimeIndex!,"AGG_FUNC","sum")
    rs!.setAttribute(playingTimeIndex!,"ALLOWED_AGG_FUNCS","sum,min,max")
    rs!.setAttribute(playingTimeIndex!,"VALUE_GETTER","Number(data.PLAYINGTIME)")
    
    grid! = new BBjGridExWidget(wnd!,100,0,40,800,560)

    cast(BBjGridExWidgetColumnsToolpanel,grid!.getSidebar().getToolpanels().get("columns")).setSuppressValues(0)
    rem grid!.setEnableValue(1)

    grid!.setShowToolPanel(1)
    grid!.setEnableRowGroup(0)
    grid!.setRowGroupPanelShow(grid!.GRID_GROUPPANEL_SHOW_VISIBLE())
    grid!.setGroupColumnLabel("My Custom Group")
    grid!.setShowGroupChildCount(1)
    grid!.setShowGroupSelectionCheckbox(1)
    grid!.setGroupSelectsChildren(1)
    
    
    rem grid!.setGroupUseEntireRow(1)
    rem grid!.setGroupMultiAutoColumn(1)
    
    
    grid!.setData(rs!,"CDNUMBER")
return

onUseCheckbox:
    useCheckbox! = cb!.isSelected()
    grid!.setFunctionsReadOnly(useCheckbox!)
return
    
byebye:
    bye
    
resize:
    ev! = BBjAPI().getLastEvent()
    grid!.setSize(ev!.getWidth(),ev!.getHeight())
return


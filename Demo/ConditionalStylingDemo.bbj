use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.SqlQueryBC
use java.sql.Types
use com.google.gson.JsonObject

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare auto BBjListButton lb_db!
declare auto BBjListButton lb_tbl!
declare auto BBjToolButton btn_fit!
declare BBjGridExWidget grid!

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,1100,600,"BBj Grid Ex Demo")

grid! = new BBjGridExWidget(wnd!,100,0,0,1100,600)

wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

gosub fillGrid

process_events

   
fillGrid:

    declare SqlQueryBC sbc!
    sbc! = new SqlQueryBC(BBjAPI().getJDBCConnection("CDStore"))
    rs! = sbc!.retrieve("SELECT  * FROM CDINVENTORY")

    grid!.addColumn("CDNUMBER","Item#")
    grid!.addColumn("ARTIST","Artist Name")
    grid!.addColumn("PLAYINGTIME","Playing Time (min)")
    grid!.addColumn("TITLE","Record Title")
    grid!.addColumn("MUSICTYPE","Gener")
    grid!.addColumn("LABEL","Publisher")
    
    grid!.setColumnBackColor("CDNUMBER",BBjAPI().makeColor("LTGRAY"))
    grid!.setColumnForeColor("CDNUMBER",BBjAPI().makeColor("BLUE"))
    grid!.pinColumn("CDNUMBER","left")
    grid!.setColumnWidth("CDNUMBER",120)
    grid!.setColumnAlignment("CDNUMBER",grid!.GRID_ALIGN_CENTER())
   
       
    generRules! = new JsonObject()
    generRules!.addProperty("gener-rock","x == 'Rock' || x== 'Classical Rock' || x=='Hard Rock'")
    
    rs!.setAttribute(6,"CELL_CLASS_RULES",generRules!.toString())
    
    rockStyle! = new JsonObject()
    rockStyle!.addProperty("background","#aaaaff")
    
    grid!.addStyle(".gener-rock" , rockStyle!)



    timesRules! = new JsonObject()
    timesRules!.addProperty("time-normal","x >= 10 && x <= 50")
    timesRules!.addProperty("time-long","x >= 50 && x <= 70")
    timesRules!.addProperty("time-tooLong","x > 70")
    
    rs!.setAttribute(4,"CELL_CLASS_RULES",timesRules!.toString())
    
    timeNormalStyle! = new JsonObject()
    timeNormalStyle!.addProperty("background","lightgreen")

    timeLongStyle! = new JsonObject()
    timeLongStyle!.addProperty("background","lightsalmon")
 
    timeTooLongStyle! = new JsonObject()
    timeTooLongStyle!.addProperty("background","lightcoral")
    
    grid!.addStyle(".time-normal" , timeNormalStyle!)
    grid!.addStyle(".time-long" , timeLongStyle!)
    grid!.addStyle(".time-tooLong" , timeTooLongStyle!)

    grid!.setMultipleSelection(0)
    grid!.setData(rs!,"CDNUMBER")       
return

byebye:
    bye
    
resize:
    ev! = BBjAPI().getLastEvent()
    grid!.setSize(ev!.getWidth(),ev!.getHeight())
return

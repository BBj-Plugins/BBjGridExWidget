use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.SqlQueryBC
use java.sql.Types
use com.google.gson.JsonObject

REM ? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare auto BBjListButton lb_db!
declare auto BBjListButton lb_tbl!
declare auto BBjToolButton btn_fit!
declare BBjGridExWidget grid!

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"BBj Grid Ex Demo")

grid! = new BBjGridExWidget(wnd!,100,0,0,800,600)

wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

gosub fillGrid

process_events

   
fillGrid:

    declare SqlQueryBC sbc!
    sbc! = new SqlQueryBC(BBjAPI().getJDBCConnection("CDStore"))
    rs! = sbc!.retrieve("SELECT  * FROM CDINVENTORY")

    grid!.addColumn("CDNUMBER","Item#")
    grid!.addColumn("ARTIST","Artist Name")
    grid!.addColumn("TITLE","Record Title")
    grid!.addColumn("LABEL","Publisher")
    grid!.addColumn("PLAYINGTIME","Playing Time (min)")
    
    grid!.setColumnBackColor("CDNUMBER",BBjAPI().makeColor("LTGRAY"))
    grid!.setColumnForeColor("CDNUMBER",BBjAPI().makeColor("BLUE"))
    grid!.pinColumn("CDNUMBER","left")
    grid!.setColumnWidth("CDNUMBER",120)
    grid!.setColumnAlignment("CDNUMBER",grid!.GRID_ALIGN_CENTER())
    
    rs!.getItem(0).setFieldAttribute("LABEL","BGCOLOR","#FF0000")
    
    REM      Another way to set styles for cells
    cellStyle! = new JsonObject()
    cellStyle!.addProperty("background","yellow")
    cellStyle!.addProperty("color","black")
    cellStyle!.addProperty("text-align","center")
    cellStyle!.addProperty("font-weight","bolder")
    cellStyle!.addProperty("font-style","italic")
    cellStyle!.addProperty("outline","3px solid green")
    
    grid!.addStyle(".my-class" , cellStyle!)
    rs!.getItem(1).setFieldAttribute("LABEL","CELL_CLASS","my-class")

    grid!.setSelectionMode(grid!.GRID_SELECT_ROW())
    grid!.setMultipleSelection(0)
    grid!.setEnableFilter(1)
    grid!.setEnableFloatingFilter(1)
    grid!.setData(rs!,"CDNUMBER")    
    grid!.moveColumn("PLAYINGTIME",1)
    grid!.setCallback(grid!.ON_GRID_SELECT_ROW(),"onRowSelect")
    grid!.setCallback(grid!.ON_GRID_DOUBLE_CLICK(),"onRowDoubleclick")

return

byebye:
    bye
    
resize:
    ev! = BBjAPI().getLastEvent()
    grid!.setSize(ev!.getWidth(),ev!.getHeight())
return

onRowSelect: 

    ev! = BBjAPI().getLastEvent()
    ev! = ev!.getObject()
    
    sel! =  grid!.getSelectedRows()
  
    if INFO(3,6)="5" then
      a = msgbox(ev!.getNewSelectedRows().toString(), 0, "Selection")
    else
      print 'SHOW', 
      print ev!.getRowCount(), " ROW(s) AFFECTED By Event:" , ev!.getChangedRows()
      print ev!.getSelectedRowCount()," ROW(S) SELECTED By Event: ",ev!.getNewSelectedRows()
      print ev!.getDeselectedRowCount()," ROW(S) DESELECTED By Event: ",ev!.getNewDeselectedRows()
      print ev!.getSelectedRows().size()," ROW(S) NOW SELECTED : ", ev!.getSelectedRows()
      print  "------------------------------------------------------------------------"
    fi

return    

onRowDoubleclick:
    ev! = BBjAPI().getLastEvent()
    ev! = ev!.getObject()
    row! = ev!.getRow()
    
    if INFO(3,6)="5" then
        a=msgbox(row!.getId() + " - " + row!.getData().get("TITLE").getAsString(),0,"DOUBLE CLICK")
    else
        print 'SHOW', "DOUBLE CLICK " , row!.getId() , " - " , row!.getData().get("TITLE").getAsString()
    fi
return  

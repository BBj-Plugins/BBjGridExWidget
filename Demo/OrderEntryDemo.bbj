use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.SqlQueryBC
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.ResultSet
use java.sql.Types

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare BBjGridExWidget grid!

x=0
y=0
w=800
h=600

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"Data Entry Demo")
wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

grid! = new BBjGridExWidget(wnd!,100,x,y,w,h)
grid!.setEditable(1)
grid!.setSingleClickEdit(1)
rem grid!.setCallback(grid!.ON_GRID_CELL_EDITING_STARTED(),"cellEditingStarted")
grid!.setCallback(grid!.ON_GRID_CELL_EDITING_STOPPED(),"cellEditingStopped")

gosub fillGrid

process_events

fillGrid:
    row! = new DataRow()

    row!.setFieldValue("ITEM_NO",java.sql.Types.VARCHAR,"")
    row!.setFieldAttribute("ITEM_NO","EDITABLE","1")
    
    row!.setFieldValue("DESC",java.sql.Types.VARCHAR,"")

    row!.setFieldValue("QTY",java.sql.Types.INTEGER,"")
    row!.setFieldAttribute("QTY","EDITABLE","1")

    row!.setFieldValue("ITEM_PRICE",java.sql.Types.INTEGER,"")
    row!.setFieldValue("LINE_PRICE",java.sql.Types.INTEGER,"")
    row!.setFieldValue("__DIRTY",java.sql.Types.INTEGER,"0")

    rs! = new ResultSet()
    rs!.add(row!)

    grid!.setData(rs!)

    rem THIS WILL NOT WORK 
    rem grid!.setData(rs!,"ITEM_NO")
return

cellEditingStarted: 
return 

cellEditingStopped:

    ev! = BBjAPI().getLastEvent()
    ev! = ev!.getObject()
    column! = ev!.getColumn()
    switch column! 
        case "ITEM_NO"
            gosub onItemNoChanged
        break
        case "QTY"
            gosub onQtyChanged
        break
    swend
return

onItemNoChanged:

    ev! = BBjAPI().getLastEvent()
    ev! = ev!.getObject()
    value$ = ev!.getValue()    

    sbc! = new SqlQueryBC(BBjAPI().getJDBCConnection("ChileCompany"))
    rs! = sbc!.retrieve("SELECT * FROM ITEM where ITEM_NUM = '" + value$ + "'")

    gridRow! = ev!.getRow()
    dataRow! = gridRow!.asDataRow()

    if rs!.count() = 0 then
        dataRow!.setFieldValue("ITEM_NO",java.sql.Types.VARCHAR,"")
        dataRow!.setFieldValue("DESC",java.sql.Types.VARCHAR,"")
        dataRow!.setFieldValue("QTY",java.sql.Types.INTEGER,"")
        dataRow!.setFieldValue("ITEM_PRICE",java.sql.Types.INTEGER,"")
        dataRow!.setFieldValue("LINE_PRICE",java.sql.Types.INTEGER,"")
        dataRow!.setFieldValue("__DIRTY",java.sql.Types.INTEGER,"1")
    else 
        rsRow! = rs!.get(0)

        dataRow!.setFieldValue("ITEM_NO",java.sql.Types.VARCHAR,value$)
        dataRow!.setFieldValue("DESC",java.sql.Types.VARCHAR,rsRow!.getFieldAsString("DESCRIPTION"))
        dataRow!.setFieldValue("QTY",java.sql.Types.INTEGER, "1")
        dataRow!.setFieldValue("ITEM_PRICE",java.sql.Types.INTEGER,rsRow!.getFieldAsNumber("PRICE"))
        dataRow!.setFieldValue("LINE_PRICE",java.sql.Types.INTEGER,rsRow!.getFieldAsNumber("PRICE"))

        if len(dataRow!.getFieldAsString("ITEM_NO")) > 0 and dataRow!.getFieldAsNumber("__DIRTY") = 0 then 

             row! = new DataRow()

            row!.setFieldValue("ITEM_NO",java.sql.Types.VARCHAR,"")
            row!.setFieldValue("DESC",java.sql.Types.VARCHAR,"")
            row!.setFieldValue("QTY",java.sql.Types.INTEGER,"")
            row!.setFieldValue("ITEM_PRICE",java.sql.Types.INTEGER,"")
            row!.setFieldValue("LINE_PRICE",java.sql.Types.INTEGER,"")
            row!.setFieldValue("__DIRTY",java.sql.Types.INTEGER,"0")
            rem THIS IS A HACK
            row!.setFieldValue("__ROW_INDEX",java.sql.Types.INTEGER,str(gridRow!.getIndex() + 1))

            grid!.addRow(gridRow!.getIndex() + 1,row!)
        fi

        dataRow!.setFieldValue("__DIRTY",java.sql.Types.INTEGER,"1")
    fi   
    
    grid!.setRowData(gridRow!.getIndex(),dataRow!)
return

onQtyChanged:

    ev! = BBjAPI().getLastEvent()
    ev! = ev!.getObject()
    value! = num(ev!.getValue())
    if value! < 1 then  
        value! = 1
    fi

    gridRow! = ev!.getRow()
    dataRow! = gridRow!.asDataRow()
    linePrice! = dataRow!.getFieldAsNumber("ITEM_PRICE") * value!

    dataRow!.setFieldValue("QTY",java.sql.Types.INTEGER, str(value!))
    dataRow!.setFieldValue("LINE_PRICE",java.sql.Types.INTEGER, str(linePrice!))
    
    grid!.setRowData(gridRow!.getIndex(),dataRow!)
return

resize:
    ev! = BBjAPI().getLastEvent()
    w=ev!.getWidth()
    h=ev!.getHeight()
    grid!.setSize(w,h)
return

byebye:
bye

use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.SqlQueryBC
use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow
use java.sql.Types
use com.google.gson.JsonObject

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare auto BBjListButton lb_db!
declare auto BBjListButton lb_tbl!
declare auto BBjToolButton btn_fit!
declare BBjGridExWidget grid!

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"BBj Grid Ex Demo")

grid! = new BBjGridExWidget(wnd!,100,0,0,800,600)
grid!.setEnableFilter(1)
grid!.setEnableFloatingFilter(1)

wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

gosub fillGrid

process_events

   
fillGrid:

    rs! = new ResultSet()
    

    i=1
    x=2222.1312345
    
    for j=1 to 2
       
        dr! = new DataRow()
        dr!.setFieldValue("ID",java.sql.Types.VARCHAR, "ID_" + STR(i))
        dr!.setFieldValue("INTEGER",java.sql.Types.INTEGER,i)
        dr!.setFieldValue("DOUBLE",java.sql.Types.DOUBLE,x)
        dr!.setFieldValue("BOOLEAN",java.sql.Types.BOOLEAN,MOD(i,3)=0)
        dr!.setFieldValue("DATE",java.sql.Types.DATE,"1979-08-03")
        dr!.setFieldValue("TIMESTAMP",java.sql.Types.TIMESTAMP,"2018-01-02 12:22:13")
        
        if i=1 then
            dr!.setFieldAttribute("DOUBLE","RENDERER_MASK","-### ##0.0000")
            
            dr!.setFieldAttribute("BOOLEAN","RENDERER_TRUE","&#x2714;")
            dr!.setFieldAttribute("BOOLEAN","RENDERER_FALSE","&#x2718;")
            
            dr!.setFieldAttribute("DATE","RENDERER_MASK","%Dz.%Mz.%Yl")
            dr!.setFieldAttribute("TIMESTAMP","RENDERER_MASK","%Dz.%Mz.%Yl %Hz:%mz:%sz")
            
            list! = new JsonObject()
            list!.addProperty("ID_1","http://www.nodeclipse.org/img/logos/Node-JS-logo.png")
            list!.addProperty("ID_3","http://www.nodeclipse.org/img/logos/Node-JS-logo.png")
            list!.addProperty("ID_2","https://jsdotgit.com/js-small.gif")
            list!.addProperty("ID_4","https://jsdotgit.com/js-small.gif")
            
            dr!.setFieldAttribute("ID","TYPE",grid!.GRID_TYPE_BASIC_IMAGE_FILTERABLE())
            dr!.setFieldAttribute("ID","IMAGES_LIST",list!.toString())
        fi
        
        rs!.addItem(dr!)
        
        i=i+1
        x=x*-i
        
        dr! = new DataRow()
        dr!.setFieldValue("ID",java.sql.Types.VARCHAR, "ID_" + STR(i))
        dr!.setFieldValue("INTEGER",java.sql.Types.INTEGER,i)
        dr!.setFieldValue("DOUBLE",java.sql.Types.DOUBLE,x)
        dr!.setFieldValue("BOOLEAN",java.sql.Types.BOOLEAN,MOD(i,3)=0)
        dr!.setFieldValue("DATE",java.sql.Types.DATE,"2013-02-01")
        dr!.setFieldValue("TIMESTAMP",java.sql.Types.TIMESTAMP,"2016-11-12 23:22:00")
        
        rs!.addItem(dr!)

        i=i+1
        x=x*-i
        
    next
    
    rem a row with most columns empty
    dr! = new DataRow()
    dr!.setFieldValue("STRING","ROW NUMBER "+str(i))
    rs!.addItem(dr!)
    
    grid!.setData(rs!)

return

byebye:
    bye
    
resize:
    ev! = BBjAPI().getLastEvent()
    grid!.setSize(ev!.getWidth(),ev!.getHeight())
return


use ::BBjGridExWidget/BBjGridExWidget.bbj::BBjGridExWidget
use com.basiscomponents.bc.SqlQueryBC
use com.basiscomponents.db.ResultSet
use com.basiscomponents.db.DataRow
use java.sql.Types
use com.google.gson.JsonObject

? 'HIDE'

declare auto BBjTopLevelWindow wnd!
declare auto BBjListButton lb_db!
declare auto BBjListButton lb_tbl!
declare auto BBjToolButton btn_fit!
declare BBjGridExWidget grid!

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"BBj Grid Ex Demo")

grid! = new BBjGridExWidget(wnd!,100,0,0,800,600)
grid!.setEnableFilter(1)
grid!.setEnableFloatingFilter(1)

wnd!        .setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!        .setCallback(BBjAPI.ON_RESIZE,"resize")

gosub prepareGrid
gosub fillGrid

process_events

   
fillGrid:

    rs! = new ResultSet()
    

    i=1
    x=2222.1312345
    
    for j=1 to 2
       
        dr! = new DataRow()
        dr!.setFieldValue("INTEGER",java.sql.Types.INTEGER,i)
        dr!.setFieldValue("ICON",java.sql.Types.VARCHAR, "ID_" + STR(i))
        dr!.setFieldValue("DOUBLE",java.sql.Types.DOUBLE,x)
        dr!.setFieldValue("BOOLEAN 1",java.sql.Types.BOOLEAN,MOD(i,3)=0)
        dr!.setFieldValue("BOOLEAN 2",java.sql.Types.BOOLEAN,MOD(i,3)=0)
        dr!.setFieldValue("DATE",java.sql.Types.DATE,"1979-08-03")
        dr!.setFieldValue("TIMESTAMP",java.sql.Types.TIMESTAMP,"2018-01-02 12:22:13")
        rs!.addItem(dr!)
        
        i=i+1
        x=x*-i
        
        dr! = new DataRow()
        dr!.setFieldValue("INTEGER",java.sql.Types.INTEGER,i)
        dr!.setFieldValue("ICON",java.sql.Types.VARCHAR, "ID_" + STR(i))
        dr!.setFieldValue("DOUBLE",java.sql.Types.DOUBLE,x)
        dr!.setFieldValue("BOOLEAN 1",java.sql.Types.BOOLEAN,MOD(i,3)=0)
        dr!.setFieldValue("BOOLEAN 2",java.sql.Types.BOOLEAN,MOD(i,3)=0)
        dr!.setFieldValue("DATE",java.sql.Types.DATE,"2013-02-01")
        dr!.setFieldValue("TIMESTAMP",java.sql.Types.TIMESTAMP,"2016-11-12 23:22:00")
        
        rs!.addItem(dr!)

        i=i+1
        x=x*-i
        
    next

    
    rem a row with most columns empty
    dr! = new DataRow()
    dr!.setFieldValue("STRING","ROW NUMBER "+str(i))
    rs!.addItem(dr!)

    grid!.setData(rs!)

return

prepareGrid:

    rem build the attributes record that describes meta data for the result set
    rem using the separate method setAttributesRecord to avoid setting this things over and over again for each refresh of the data
    
    ar! = new DataRow()
    ar!.setFieldValue("INTEGER",java.sql.Types.INTEGER,i)
    ar!.setFieldValue("ICON",java.sql.Types.VARCHAR, "ID_" + STR(i))
    ar!.setFieldValue("DOUBLE",java.sql.Types.DOUBLE,x)
    ar!.setFieldValue("BOOLEAN 1",java.sql.Types.BOOLEAN,MOD(i,3)=0)
    ar!.setFieldValue("BOOLEAN 2",java.sql.Types.BOOLEAN,MOD(i,3)=0)
    ar!.setFieldValue("DATE",java.sql.Types.DATE,"2013-02-01")
    ar!.setFieldValue("TIMESTAMP",java.sql.Types.TIMESTAMP,"2016-11-12 23:22:00")

    ar!.setFieldAttribute("DOUBLE","RENDERER_MASK","-### ##0.0000"); rem or RENDERER_MASK
    ar!.setFieldAttribute("DOUBLE","LABEL","A Double Value")

    REM this is also the default:    
    ar!.setFieldAttribute("BOOLEAN 1","RENDERER_TRUE","&#x2714;")
    ar!.setFieldAttribute("BOOLEAN 1","RENDERER_FALSE","&#x2718;")
    
    ar!.setFieldAttribute("BOOLEAN 2","RENDERER_TRUE","switch")
    ar!.setFieldAttribute("BOOLEAN 2","RENDERER_FALSE","switch")
    
    ar!.setFieldAttribute("DATE","MASK","%Dz.%Mz.%Yl"); rem // or RENDERER_MASK
    ar!.setFieldAttribute("TIMESTAMP","RENDERER_MASK","%Dz.%Mz.%Yl %Hz:%mz:%sz")
    
    list! = new JsonObject()
    list!.addProperty("ID_1","https://raw.githubusercontent.com/google/material-design-icons/master/notification/1x_web/ic_airline_seat_individual_suite_black_36dp.png")
    list!.addProperty("ID_2","https://raw.githubusercontent.com/google/material-design-icons/master/notification/1x_web/ic_confirmation_number_black_36dp.png")
    list!.addProperty("ID_3","https://raw.githubusercontent.com/google/material-design-icons/master/notification/1x_web/ic_drive_eta_black_36dp.png")
    list!.addProperty("ID_4","https://raw.githubusercontent.com/google/material-design-icons/master/notification/1x_web/ic_adb_black_36dp.png")
    
    ar!.setFieldAttribute("ICON","TYPE",grid!.GRID_TYPE_BASIC_IMAGE_FILTERABLE())
    ar!.setFieldAttribute("ICON","IMAGES_LIST",list!.toString())

    grid!.setAttributesRecord(ar!)

return

byebye:
    bye
    
resize:
    ev! = BBjAPI().getLastEvent()
    grid!.setSize(ev!.getWidth(),ev!.getHeight())
return


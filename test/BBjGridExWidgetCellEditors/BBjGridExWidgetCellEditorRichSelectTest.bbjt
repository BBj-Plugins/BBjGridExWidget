rem /**
rem  * This file is part of the BBjGridExWidget plugin.
rem  * (c) Basis Europe <eu@basis.com>
rem  *
rem  * For the full copyright and license information, please view the LICENSE
rem  * file that was distributed with this source code.
rem  */

use ::BBjGridExWidget/BBjGridExWidgetCellEditors.bbj::BBjGridExWidgetCellEditorRichSelect
use ::BBjGridExWidget/BBjGridExWidgetRenderers.bbj::BBjGridExWidgetRendererCustomHTML
use ::BBjGridExWidget/BBjGridExWidgetCellEditors.bbj::BBjGridExWidgetCellEditorSelect
use com.google.gson.JsonObject
use com.google.gson.JsonNull

class public BBjGridExWidgetCellEditorRichSelectTest

    field private BBjGridExWidgetCellEditorRichSelect Instance!
    
    rem @Before
    method public void setUp()
        #Instance! = new BBjGridExWidgetCellEditorRichSelect()
    methodend

    rem @Test
    method public void testCellEditorNameIsSet()
        Assert.Equals(#Instance!.getCellEditorName() , "agRichSelectCellEditor")
    methodend
    
    rem @Test
    method public void testCanSetCellRenderer()
        declare JsonObject json!
        
        json! = #Instance!.getAsJsonObject()
        Assert.Equals(json!.has("cellRenderer"), BBjAPI.FALSE)
        
        renderer! = new BBjGridExWidgetRendererCustomHTML("<strong>value</strong>")
        #Instance!.setCellRenderer(renderer!)
        
        json! = #Instance!.getAsJsonObject()
        Assert.Equals(json!.get("cellRenderer").getAsString(), renderer!.getCellRendererName())
        Assert.Equals(json!.get("cellRendererParams").getAsJsonObject().equals(renderer!.getAsJsonObject()), BBjAPI.TRUE)
        
    methodend
            
    rem @Test
    method public void testFallbackToBBjGridExWidgetCellEditorSelect()
        proxy! = new BBjGridExWidgetCellEditorRichSelectProxy()
        Assert.Equals(proxy!.getCellEditorName() , BBjGridExWidgetCellEditorSelect.getCellEditorName())
    methodend   
                
    rem @After
    method public void tearDown()
        #Instance! = null()
    methodend

classend

class public BBjGridExWidgetCellEditorRichSelectProxy extends BBjGridExWidgetCellEditorRichSelect
    rem /**
    rem  * @override
    rem  */
    method protected static BBjNumber isBUI()
        methodret 1
    methodend
classend

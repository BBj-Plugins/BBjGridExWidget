rem /**
rem  * This file is part of the BBjGridExWidget plugin.
rem  * (c) Basis Europe <eu@basis.com>
rem  *
rem  * For the full copyright and license information, please view the LICENSE
rem  * file that was distributed with this source code.
rem  */
use ::BBjGridExWidget/BBjGridExWidgetClientModels.bbj::BBjGridExWidgetClientFilterBooleanModel
use com.google.gson.JsonObject
use com.google.gson.JsonArray
use com.google.gson.JsonNull

class public BBjGridExWidgetClientFilterBooleanModelTest

    field private BBjGridExWidgetClientFilterBooleanModel Instance!
    
    rem @Before
    method public void setUp()
        #Instance! = new BBjGridExWidgetClientFilterBooleanModel()
    methodend
    
    rem @Test
    method public void testConstructors()
        declare BBjGridExWidgetClientFilterBooleanModel obj!
        
        rem two params
        obj! = new BBjGridExWidgetClientFilterBooleanModel(1)
        Assert.Equals(obj!.getFilter() , 1)
        
        rem no params
        obj! = new BBjGridExWidgetClientFilterBooleanModel()
        Assert.IsNull(obj!.getFilter())   
    methodend
    
    rem @Test
    method public void testCanSetFilter()
        declare JsonObject json!
        
        json! = #Instance!.getAsJsonObject()
        Assert.Equals(json!.has("value"), BBjAPI.FALSE)
        
        #Instance!.setFilter(0)
        json! = #Instance!.getAsJsonObject()
        
        Assert.Equals(json!.get("value").getAsString(), "false")
        
        #Instance!.setFilter(1)
        json! = #Instance!.getAsJsonObject()
        
        Assert.Equals(json!.get("value").getAsString(), "true")        
    methodend  
    
    rem @Test
    method public void testClearFilter()
        #Instance!.setFilter(1)
        #Instance!.clearFilter()
        Assert.IsNull(#Instance!.getFilter())
    methodend      
     
    rem @After
    method public void tearDown()
        #Instance! = null()
    methodend

classend

rem /**
rem  * This file is part of the BBjGridExWidget plugin.
rem  * (c) Basis Europe <eu@basis.com>
rem  *
rem  * For the full copyright and license information, please view the LICENSE
rem  * file that was distributed with this source code.
rem  */

use ::BBjGridExWidget/GxCellEditors.bbj::GxCellEditorRichSelect
use ::BBjGridExWidget/GxRenderers.bbj::GxRendererCustomHTML
use ::BBjGridExWidget/GxCellEditors.bbj::GxCellEditorSelect
use com.google.gson.JsonObject
use com.google.gson.JsonNull

class public GxCellEditorRichSelectTest

    field private GxCellEditorRichSelect Instance!
    
    rem @Before
    method public void setUp()
        #Instance! = new GxCellEditorRichSelect()
    methodend

    rem @Test
    method public void testCellEditorNameIsSet()
        Assert.Equals(#Instance!.getCellEditorName() , "agRichSelectCellEditor")
    methodend
    
    rem @Test
    method public void testCanSetCellRenderer()
        declare JsonObject json!
        
        json! = #Instance!.getAsJsonObject()
        Assert.Equals(json!.has("cellRenderer"), BBjAPI.FALSE)
        
        renderer! = new GxRendererCustomHTML("<strong>value</strong>")
        #Instance!.setCellRenderer(renderer!)
        
        json! = #Instance!.getAsJsonObject()
        Assert.Equals(json!.get("cellRenderer").getAsString(), renderer!.getCellRendererName())
        Assert.Equals(json!.get("cellRendererParams").getAsJsonObject().equals(renderer!.getAsJsonObject()), BBjAPI.TRUE)
        
    methodend
            
    rem @Test
    method public void testFallbackToGxCellEditorSelect()
        proxy! = new GxCellEditorRichSelectProxy()
        Assert.Equals(proxy!.getCellEditorName() , GxCellEditorSelect.getCellEditorName())
    methodend   
                
    rem @After
    method public void tearDown()
        #Instance! = null()
    methodend

classend

class public GxCellEditorRichSelectProxy extends GxCellEditorRichSelect
    rem /**
    rem  * @override
    rem  */
    method protected static BBjNumber isBUI()
        methodret 1
    methodend
classend
